AppType=StandardJava
Build1=Default,my.b4j.api
File1=config.properties
File2=index.html
File3=main.html
File4=password.html
File5=user.html
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
Group=Default Group
Library1=jcore
Library2=jserver
Library3=jsql
Library4=jokhttputils2
Library5=json
Library6=jnet
Library7=byteconverter
Library8=encryption
Library9=javaobject
Module1=HomeHandler
Module2=HttpsFilter
Module3=PasswordHandler
Module4=TestHandler
Module5=UserHandler
Module6=Utility
Module7=WebUtils
NumberOfFiles=5
NumberOfLibraries=9
NumberOfModules=7
Version=8.5
@EndOfDesignText@
'Non-UI application (console / server application)
#Region  Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True 
#End Region

' MySQL Connector
#AdditionalJar: mysql-connector-java-5.1.37-bin
'#AdditionalJar: postgresql-9.4.1207				' PostgreSQL
'#AdditionalJar: jtds-1.3.1.jar						' SQL Server
	
Sub Process_Globals
	Public srvr As Server
	Public SERVER_PORT As Int
	Public SSL_PORT As Int
	Public ROOT_PATH As String
	Public ROOT_URL As String
	'Private ELEMENT_ROOT As Int
	Public ELEMENT_CONTROLLER As Int
	Public ELEMENT_ACTION As Int
	Public ELEMENT_ID As Int
	Public MAX_ELEMENTS As Int = 4
	Public const VERSION As Float = 1.02
End Sub

Sub AppStart (Args() As String)
	Dim config As Map = Utility.ReadConfig
	SERVER_PORT = config.Get("ServerPort")
	SSL_PORT = config.Get("SSLPort")
	ROOT_PATH = config.Get("ROOT_PATH")
	ROOT_URL = config.Get("ROOT_URL") & ":" & SERVER_PORT

	srvr.Initialize("")
	srvr.Port = SERVER_PORT
	#If RELEASE
	ConfigureSSL(SSL_PORT)
	ROOT_URL = config.Get("ROOT_URL") & ":" & SSL_PORT
	#End If
	srvr.StaticFilesFolder = File.Combine(File.DirApp, "www")
	srvr.SetStaticFilesOptions(CreateMap("dirAllowed": False))

	If ROOT_PATH = "/" Then
		srvr.AddHandler("", "HomeHandler", False) ' using subdomain?
		'ELEMENT_ROOT = 0
		ELEMENT_CONTROLLER = 1
		ELEMENT_ACTION = 2
		ELEMENT_ID = 3
	Else
		srvr.AddHandler(ROOT_PATH, "HomeHandler", False)
		'ELEMENT_ROOT = 1
		ELEMENT_CONTROLLER = 2
		ELEMENT_ACTION = 3
		ELEMENT_ID = 4
		MAX_ELEMENTS = MAX_ELEMENTS + 1
	End If	

	srvr.AddHandler(ROOT_PATH & "test/*", "TestHandler", False)
	srvr.AddHandler(ROOT_PATH & "user/*", "UserHandler", False)
	srvr.AddHandler(ROOT_PATH & "password/*", "PasswordHandler", False)
	srvr.Start
	
	Log($"API server (version = $1.2{VERSION}) is running on port ${srvr.Port}"$)
	#If RELEASE
	Log($"Response will be redirected to port ${srvr.SslPort}"$)
	#End If
	LogDebug($"Open the following URL from your web browser"$)
	LogDebug(ROOT_URL)	
	StartMessageLoop
End Sub

Private Sub ConfigureSSL (SslPort As Int)
	'example of SSL connector configuration
	Dim ssl As SslConfiguration
	ssl.Initialize
	ssl.SetKeyStorePath("/etc/letsencrypt/live/api.puterise.com", "keystore.jks") 'path to keystore file
	ssl.KeyStorePassword = "123456"
	'ssl.KeyManagerPassword = "654321"
	srvr.SetSslConfiguration(ssl, SslPort)
	'add filter to redirect all traffic from http to https (optional)
	srvr.AddFilter("/*", "HttpsFilter", False)
End Sub